//
// Main build file for AtoMS
apply plugin: "java"
apply plugin: "maven"

group = "org.oddcyb"
version = getVersion()

task run (type: JavaExec, dependsOn: classes){
    if(project.hasProperty('appargs')){
        args(appargs.split(','))
    }
    description = "Run AtoMS Service"
    main = "org.oddcyb.atoms.Start"
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes "Main-Class": "org.oddcyb.atoms.Start"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.sparkjava:spark-core:2.5.3"
    compile "net.sf.jopt-simple:jopt-simple:6.0-alpha-1"
    compile "com.mashape.unirest:unirest-java:1.4.9"
    compile "org.mapdb:mapdb:3.0.2"
    compile "org.mongodb:mongodb-driver:3.4.0"
    runtime "org.slf4j:slf4j-simple:1.7.21"
}

/**
 * Gets the version name from the latest Git tag.
 */
def getVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
        
    return stdout.toString().trim()
}


