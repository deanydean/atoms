//
// Main build file for AtoMS
apply plugin: "java"
apply plugin: "maven"

def getVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group = "org.oddcyb"
version = getVersion()

task run (type: JavaExec, dependsOn: classes){
    if(project.hasProperty('appargs')){
        args(appargs.split(','))
    }
    description = "Run AtoMS Service"
    main = "org.oddcyb.atoms.Start"
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes "Main-Class": "org.oddcyb.atoms.Start"
    }

    from {
        configurations.compile.collect { 
            it.isDirectory() ? it : zipTree(it).matching {
                exclude { it.path.contains("META-INF") }
            }
        }
        
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude { it.path.contains("META-INF") }
            }
        }
    }

    archiveName = "atoms.jar"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.gson:gson:2.8.2"
    compile "com.sparkjava:spark-core:2.7.2"
    compile "net.sf.jopt-simple:jopt-simple:6.0-alpha-2"
    compile "com.mashape.unirest:unirest-java:1.4.9"
    compile "org.mapdb:mapdb:3.0.5"
    compile "org.mongodb:mongodb-driver:3.6.3"
    runtime "org.slf4j:slf4j-simple:1.7.25"
}




